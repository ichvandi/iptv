plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.vandoc.iptv"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "BASE_URL", '"http://192.168.0.2:8080/"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Other Parameters
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
//            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = compose_version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Coroutines
    def coroutinesVersion = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    // Lifecycle
    def lifecycleVersion = "2.6.0-alpha01"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    // Compose
    def composeUiVersion = "1.3.0-alpha03"
    implementation "androidx.compose.ui:ui:$composeUiVersion"
    implementation "androidx.compose.material:material:$composeUiVersion"
    implementation "androidx.compose.material3:material3:1.0.0-alpha16"
    implementation "androidx.compose.ui:ui-tooling-preview:$composeUiVersion"
    implementation "androidx.compose.material:material-icons-extended:$composeUiVersion"
    implementation "androidx.compose.ui:ui-viewbinding:$composeUiVersion"
    debugImplementation "androidx.compose.ui:ui-tooling:$composeUiVersion"

    // Compose Navigation
    def composeNavigationVersion = "1.6.12-beta"
    implementation "io.github.raamcosta.compose-destinations:core:$composeNavigationVersion"
    ksp "io.github.raamcosta.compose-destinations:ksp:$composeNavigationVersion"

    // Compose Image Loader
    def composeLandscapistVersion = "1.5.2"
    implementation "com.github.skydoves:landscapist-coil:$composeLandscapistVersion"

    // Compose Collapsing Toolbar
    def composeCollapsingToolbarVersion = "2.3.4"
    implementation "me.onebone:toolbar-compose:$composeCollapsingToolbarVersion"

    // Compose DI
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Dependency Injection
    implementation 'com.google.dagger:hilt-android:2.42'
    kapt 'com.google.dagger:hilt-compiler:2.42'

    // Networking
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Exoplayer
    def exoplayerVersion = "2.18.1"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoplayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayerVersion"
    implementation "com.google.android.exoplayer:extension-okhttp:$exoplayerVersion"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    implementation "com.squareup.okhttp3:okhttp-dnsoverhttps:4.10.0"

    // Chucker
    debugImplementation "com.github.chuckerteam.chucker:library:3.5.2"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:3.5.2"

    // Remote Config
    def firebaseVersion = "21.1.1"
    implementation "com.google.firebase:firebase-config-ktx:$firebaseVersion"

    // Room
    def roomVersion = "2.4.3"
    implementation "androidx.room:room-runtime:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"

    // Work Manager
    def workVersion = "2.7.1"
    implementation "androidx.work:work-runtime-ktx:$workVersion"

    // Work Manager DI
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}